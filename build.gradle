plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.breadmoirai.github-release' version '2.2.10'
    
}

repositories {
	mavenCentral()
	mavenLocal()
}

jar {
  enabled = true
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'    
	implementation ('org.springframework.boot:spring-boot-starter-batch') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-jdbc'
	}
	implementation 'org.springframework:spring-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
	implementation 'com.redislabs:spring-redisearch:1.12.8'
	implementation 'org.glassfish.jaxb:jaxb-runtime'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	implementation 'com.thoughtworks.xstream:xstream:1.4.11.1'
}

test {
    useJUnitPlatform()
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : ''
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : ''
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'org.ruaux:jdiscogs'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/jruaux/jdiscogs.git'
	    version {
	        gpg {
	            sign = true
	        }
	        mavenCentralSync {
	            sync = true
	            user = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : ''
	            password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : ''
	        }
	    }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'org.ruaux'
            artifactId 'jdiscogs'

            pom {
                name = 'JDiscogs'
                packaging = 'jar'                
                description = 'Java client for Discogs API and Data'
                url = 'https://github.com/jruaux/jdiscogs'

		        scm {
		          connection = 'scm:git:git://github.com/jruaux/jdiscogs.git'
		          developerConnection = 'scm:git:git@github.com:jruaux/jdiscogs.git'
		          url = 'https://github.com/jruaux/jdiscogs'
		        }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
            }
        }
    }
}

group = 'org.ruaux'
description = 'JDiscogs'
sourceCompatibility = '1.8'

task printVersion {
    doLast {
        println project.version
    }
}

githubRelease {
    token = project.hasProperty('githubToken') ? project.property('githubToken') : ''
    owner "jruaux"
    repo "jdiscogs"
}

afterReleaseBuild.dependsOn ":githubRelease"
afterReleaseBuild.dependsOn bintrayUpload
