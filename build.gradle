plugins {
    id 'com.jfrog.bintray' version '1.8.5'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'application'
    id 'java-library'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.8.1'
}

group = 'org.ruaux'
description = 'JDiscogs'
sourceCompatibility = '1.8'

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

application {
    mainClassName = 'org.ruaux.jdiscogs.JDiscogsApplication'
    applicationName = 'jdiscogs'
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.hsqldb:hsqldb'
    api 'org.springframework:spring-web'
    implementation 'org.springframework:spring-oxm'
    implementation 'org.glassfish.jaxb:jaxb-runtime'
    implementation 'com.redislabs:spring-redisearch:2.3.2'
    implementation 'com.redislabs:spring-batch-redisearch:2.4.3'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.testcontainers:testcontainers:1.14.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.14.3'
}

test {
    useJUnitPlatform()
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : ''
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : ''
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'jdiscogs'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/jruaux/jdiscogs.git'
        version {
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : ''
                password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : ''
            }
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'org.ruaux'
            artifactId 'jdiscogs'

            pom {
                name = 'JDiscogs'
                packaging = 'jar'
                description = 'Java client for Discogs API and Data'
                url = 'https://github.com/jruaux/jdiscogs'

                scm {
                    connection = 'scm:git:git://github.com/jruaux/jdiscogs.git'
                    developerConnection = 'scm:git:git@github.com:jruaux/jdiscogs.git'
                    url = 'https://github.com/jruaux/jdiscogs'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
            }
        }
    }
}

afterReleaseBuild.dependsOn bintrayUpload
