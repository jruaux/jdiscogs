plugins {
    id 'java'
    id 'application'
    id 'java-library'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.33.0'
	id 'com.github.breadmoirai.github-release' version '2.2.12'    
    id 'com.jfrog.bintray' version '1.8.5'
}

group = 'org.ruaux'
description = 'JDiscogs'
sourceCompatibility = '1.8'

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

application {
    mainClassName = 'org.ruaux.jdiscogs.JDiscogsApplication'
    applicationName = 'jdiscogs'
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
    implementation ('org.springframework.boot:spring-boot-starter-batch:2.3.5.RELEASE') {
    	exclude group: 'io.lettuce', module: 'lettuce-core'
    }
    implementation 'org.hsqldb:hsqldb:2.5.1'
    api 'org.springframework:spring-web:5.3.0'
    implementation 'org.springframework:spring-oxm:5.3.0'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.3'
    implementation 'com.redislabs:spring-batch-redis:2.3.3'
    implementation 'com.redislabs:spring-redisearch:2.4.4'
    implementation 'com.redislabs:spring-batch-redisearch:2.4.4'
    testImplementation('org.springframework.boot:spring-boot-starter-test:2.3.5.RELEASE') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.batch:spring-batch-test:4.3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testImplementation 'org.testcontainers:testcontainers:1.15.0-rc2'
    testImplementation 'org.testcontainers:junit-jupiter:1.15.0-rc2'
}

test {
    useJUnitPlatform()
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : ''
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : ''
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'jdiscogs'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/jruaux/jdiscogs.git'
        version {
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : ''
                password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : ''
            }
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'org.ruaux'
            artifactId 'jdiscogs'

            pom {
                name = 'JDiscogs'
                packaging = 'jar'
                description = 'Java client for Discogs API and Data'
                url = 'https://github.com/jruaux/jdiscogs'

                scm {
                    connection = 'scm:git:git://github.com/jruaux/jdiscogs.git'
                    developerConnection = 'scm:git:git@github.com:jruaux/jdiscogs.git'
                    url = 'https://github.com/jruaux/jdiscogs'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
            }
        }
    }
}

githubRelease {
    token = project.hasProperty('githubToken') ? project.property('githubToken') : ''
    owner "jruaux"
    repo "jdiscogs"
    body changelog()
}


afterReleaseBuild.dependsOn ":githubRelease"
afterReleaseBuild.dependsOn bintrayUpload
